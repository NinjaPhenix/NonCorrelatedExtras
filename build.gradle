import groovy.json.JsonOutput
import groovy.json.JsonSlurper

plugins {
    id 'fabric-loom' version '0.7-SNAPSHOT'
}

allprojects {
    apply plugin: 'fabric-loom'

    project.setVersion(project.properties.mod_version)
    project.setGroup(project.properties.maven_group)
    project.setArchivesBaseName(project.properties.archives_base_name as String)

    repositories {
        maven {
            name = 'Shedaniel'
            url 'https://maven.shedaniel.me/'
        }
        maven {
            name = 'TerraformersMC'
            url = 'https://maven.terraformersmc.com/'
        }
    }

    java {
        withSourcesJar()
        toolchain {
            languageVersion = JavaLanguageVersion.of(project.properties.source_java_version as int)
        }
    }

    dependencies {
        minecraft("com.mojang:minecraft:${project.properties.minecraft_version}")
        mappings(loom.officialMojangMappings())
        modImplementation("net.fabricmc:fabric-loader:${project.properties.fabric_loader_version}")
        modImplementation("net.fabricmc.fabric-api:fabric-api:${project.properties.fabric_api_version}")
    }

    processResources {
        def props = ['version'              : project.properties.version,
                     'nce_version'          : rootProject.properties.version,
                     'minecraft_dependency' : rootProject.properties.minecraft_dependency,
                     'mod_license'          : project.properties.mod_license,
                     'modmenu_parent_object': """
{
  "id": "noncorrelatedextras",
  "name": "NonCorrelatedExtras",
  "description": "See sub-modules for more details about features."
}
"""]
        inputs.properties props

        filesMatching('fabric.mod.json') {
            expand props
        }

        // MIT License, Copyright 2021 shedaniel (https://github.com/shedaniel)
        // https://github.com/shedaniel/cloth-config-lite/blob/615f6ed8ebebb8cb973d4901565558ddc486e44c/build.gradle#L52-L57
        // Minifies json files, todo: does not minify mixin json
        doLast {
            def slurper = new JsonSlurper()
            fileTree(dir: outputs.files.asPath, include: "**/*.json").each {
                File file -> file.text = JsonOutput.toJson(slurper.parse(file))
            }
        }
    }
}

subprojects {
    dependencies {
        afterEvaluate {
            compileOnly rootProject
        }

        //modCompile "com.github.emilyploszaj:trinkets:v2.3.0"
    }
}

dependencies {
    modRuntime('me.shedaniel:RoughlyEnoughItems:5.11.202')
    modRuntime('com.terraformersmc:modmenu:1.16.9')

    for (def proj : subprojects) {
        runtimeOnly(proj)
        include(proj)
    }
}
